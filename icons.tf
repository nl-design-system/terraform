resource "github_repository" "icons" {
  name                        = "icons"
  description                 = "Work in Progress: Open source icons that can be used in the NL Design System community."
  allow_merge_commit          = false
  allow_rebase_merge          = true
  allow_squash_merge          = true
  allow_auto_merge            = true
  delete_branch_on_merge      = true
  has_issues                  = true
  has_downloads               = false
  has_projects                = true
  has_wiki                    = false
  vulnerability_alerts        = true
  homepage_url                = "https://nl-design-system.github.io/icons/"
  squash_merge_commit_title   = "PR_TITLE"
  squash_merge_commit_message = "PR_BODY"
  topics                      = ["nl-design-system", "icons"]

  template {
    include_all_branches = false
    owner                = "nl-design-system"
    repository           = "example"
  }

  lifecycle {
    prevent_destroy = true
  }
}

resource "github_branch_default" "icons" {
  branch     = "main"
  repository = github_repository.icons.name
}

resource "github_repository_ruleset" "cons-main" {
  enforcement = "active"
  name        = "default-branch-protection"
  repository  = github_repository.icons.name
  target      = "branch"

  conditions {
    ref_name {
      include = ["~DEFAULT_BRANCH"]
      exclude = []
    }
  }

  rules {
    creation                      = true
    deletion                      = true
    non_fast_forward              = true
    required_linear_history       = true
    required_signatures           = false
    update                        = false
    update_allows_fetch_and_merge = false

    pull_request {
      dismiss_stale_reviews_on_push     = true
      require_code_owner_review         = true
      require_last_push_approval        = false
      required_approving_review_count   = 1
      required_review_thread_resolution = true
    }

    required_status_checks {
      do_not_enforce_on_create             = false
      strict_required_status_checks_policy = false

      required_check {
        context = "install"
      }

      required_check {
        context = "lint"
      }

      required_check {
        context = "test"
      }

      required_check {
        context = "build"
      }
    }
  }
}


resource "github_repository_collaborators" "icons" {
  repository = github_repository.icons.name

  team {
    permission = "admin"
    team_id    = github_team.kernteam-admin.id
  }

  team {
    permission = "maintain"
    team_id    = github_team.kernteam-maintainer.id
  }

  team {
    permission = "push"
    team_id    = github_team.kernteam-committer.id
  }

  team {
    permission = "triage"
    team_id    = github_team.kernteam-triage.id
  }

  team {
    permission = "triage"
    team_id    = github_team.kernteam-dependabot.id
  }

  team {
    permission = "push"
    team_id    = github_team.icons-committer.id
  }
}

resource "vercel_project" "icons" {
  name             = github_repository.icons.name
  output_directory = "packages/storybook/dist/"
  ignore_command   = "[[ $(git log -1 --pretty=%an) == 'dependabot[bot]' ]]"

  git_repository = {
    type = "github"
    repo = "${data.github_organization.nl-design-system.orgname}/${github_repository.icons.name}",
  }

  vercel_authentication = {
    deployment_type = "none"
  }
}
