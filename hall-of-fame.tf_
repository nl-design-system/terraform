resource "github_repository" "hall-of-fame" {
  name                        = "hall-of-fame"
  description                 = "NL Design System Hall of Fame components"
  allow_merge_commit          = false
  allow_rebase_merge          = true
  allow_squash_merge          = true
  allow_auto_merge            = true
  delete_branch_on_merge      = true
  has_issues                  = true
  has_downloads               = false
  has_projects                = false
  has_wiki                    = false
  vulnerability_alerts        = true
  homepage_url                = "https://nl-design-system.github.io/hall-of-fame/"
  squash_merge_commit_title   = "PR_TITLE"
  squash_merge_commit_message = "PR_BODY"
  topics                      = ["nl-design-system", "storybook"]
  visibility                  = "private"


  template {
    owner                = "nl-design-system"
    repository           = "example"
    include_all_branches = false
  }

  security_and_analysis {
    secret_scanning {
      status = "enabled"
    }
    secret_scanning_push_protection {
      status = "enabled"
    }
  }

  pages {
    build_type = "workflow"
  }

  lifecycle {
    prevent_destroy = true
  }
}

resource "github_branch_default" "hall-of-fame" {
  repository = github_repository.hall-of-fame.name
  branch     = "main"
}

resource "github_repository_ruleset" "hall-of-fame-main" {
  enforcement = "active"
  name        = "default-branch-protection"
  repository  = github_repository.hall-of-fame.name
  target      = "branch"

  bypass_actors {
    actor_id    = github_team.kernteam-ci.id
    actor_type  = "Team"
    bypass_mode = "always"
  }

  conditions {
    ref_name {
      include = ["~DEFAULT_BRANCH"]
      exclude = []
    }
  }

  rules {
    creation                      = false
    deletion                      = true
    non_fast_forward              = true
    required_linear_history       = true
    required_signatures           = false
    update                        = false
    update_allows_fetch_and_merge = false

    pull_request {
      dismiss_stale_reviews_on_push     = true
      require_code_owner_review         = true
      require_last_push_approval        = false
      required_approving_review_count   = 1
      required_review_thread_resolution = true
    }

    required_status_checks {
      strict_required_status_checks_policy = false

      required_check {
        context = "build"
      }
      required_check {
        context = "install"
      }
      required_check {
        context = "lint"
      }
      required_check {
        context = "test"
      }
    }
  }
}

resource "github_repository_collaborators" "hall-of-fame" {
  repository = github_repository.hall-of-fame.name

  team {
    permission = "admin"
    team_id    = github_team.kernteam-admin.slug
  }

  team {
    permission = "maintain"
    team_id    = github_team.kernteam-maintainer.slug
  }

  team {
    permission = "push"
    team_id    = github_team.kernteam-committer.slug
  }

  team {
    permission = "triage"
    team_id    = github_team.kernteam-triage.slug
  }

  team {
    permission = "triage"
    team_id    = github_team.kernteam-dependabot.slug
  }
}

resource "vercel_project" "hall-of-fame" {
  name                    = github_repository.hall-of-fame.name
  output_directory        = "packages/storybook/dist"
  ignore_command          = "[[ $(git log -1 --pretty=%an) == 'dependabot[bot]' ]]"
  enable_preview_feedback = false

  git_repository = {
    type = "github"
    repo = "${data.github_organization.nl-design-system.orgname}/${github_repository.hall-of-fame.name}"
  }

  vercel_authentication = {
    deployment_type = "none"
  }
}

resource "vercel_project" "hall-of-fame-storybook-test" {
  name                    = "hall-of-fame-storybook-test"
  output_directory        = "packages/storybook-test/dist/"
  ignore_command          = "[[ $(git log -1 --pretty=%an) == 'dependabot[bot]' ]]"
  enable_preview_feedback = false

  git_repository = {
    type = "github"
    repo = "${data.github_organization.nl-design-system.orgname}/${github_repository.hall-of-fame.name}"
  }

  vercel_authentication = {
    deployment_type = "none"
  }
}
